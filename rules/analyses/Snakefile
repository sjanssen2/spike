rule biom_gatkbackground:
    # Realign the the reads
    input:
        "{prefix}%s%s/{sample}.{program}{isrelax}.snp_indel.vcf" % (config['dirs']['intermediate'], config['stepnames']['gatk_CombineVariants'])
    output:
        "{prefix}%s%s/{sample}.{program,gatk}{isrelax,\.relax|}.snp_indel.biom" % (config['dirs']['intermediate'], config['stepnames']['biom_gatkbackground'])
    log:
        "{prefix}%s%s/{sample}.{program}{isrelax}.log" % (config['dirs']['logs'], config['stepnames']['biom_gatkbackground'])
    benchmark:
        "{prefix}%s%s/{sample}.{program}{isrelax}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['biom_gatkbackground'])
    threads:
        1
    run:
        sample_to_biom(input[0], output[0], wildcards.sample)


rule biom_merge:
    input:
        lambda wildcards: ["%s%s%s/%s.gatk.snp_indel.biom" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['biom_gatkbackground'], sample['sample'])
                           for sample in get_samples(SAMPLESHEETS, config)
                           if sample['Sample_Project'] == wildcards.project]
    output:
        "{prefix}%s%s/{project}.biom" % (config['dirs']['intermediate'], config['stepnames']['biom_merged'])
    log:
        "{prefix}%s%s/{project}.log" % (config['dirs']['logs'], config['stepnames']['biom_merged'])
    benchmark:
        "{prefix}%s%s/{project}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['biom_merged'])
    threads:
        1
    run:
        merge_samples(input, output[0], wildcards.project)


rule install_bamstat:
    output:
        "{prefix}%sbamstat04.jar" % (config['dirs']['references'])
    conda:
        "envs/spike_bamstats.yaml"
    log:
        "{prefix}%sinstall_bamstat.log" % (config['dirs']['logs'])
    benchmark:
        "{prefix}%sinstall_bamstat.log" % (config['dirs']['benchmarks'])
    threads:
        1
    shell:
        'workdir=`mktemp -d` > {log} 2>&1'
        ' && echo "workdir is $workdir" >> {log}'
        ' && cd $workdir >> {log} 2>&1'
        ' && git clone "https://github.com/lindenb/jvarkit.git" >> {log} 2>&1'
        ' && cd jvarkit 2>> {log}'
        #' && make bamstats04 standalone=yes >> {log} 2>&1'
        ' && ./gradlew bamstats04 >> {log} 2>&1'#new makefile
        ' && cp -v dist/bamstats04.jar {output} >> {log} 2>&1'
        ' && cd 2>> {log}'
        ' && rm -rfv $workdir >> {log} 2>&1'


rule bamstat:
    input:
        bam="{prefix}%s%s/{sample}.reCal.reAl.nodup.srt.bam" % (config['dirs']['intermediate'], config['stepnames']['gatk_PrintReads']),
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(wildcards.sample, SAMPLESHEETS, config)),
        bin_bamstat="{prefix}%sbamstat04.jar" % (config['dirs']['references']),
    output:
        "{prefix}%s%s/{sample}.bamstat.tsv" % (config['dirs']['intermediate'], config['stepnames']['bamstat'])
    conda:
        "envs/spike_bamstats.yaml"
    log:
        "{prefix}%s%s/{sample}.log" % (config['dirs']['logs'], config['stepnames']['bamstat'])
    benchmark:
        "{prefix}%s%s/{sample}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['bamstat'])
    threads:
        1
    shell:
        "h=`mktemp`"
        ' && echo "tempfile is $h" > {log} 2>&1'
        " && java -jar {input.bin_bamstat} -B {input.exometrack} {input.bam} 2>> {log} > $h"
        " && column -t $h > {output} 2>> {log}"
        " && rm -f $h 2>>{log}"


rule genepanel_coverage:
    input:
        agilent=lambda wildcards: '%s%s%s' % (wildcards.prefix, config['dirs']['references'], get_reference_exometrack(wildcards.sample, SAMPLESHEETS, config, returnfield="agilent_coverage_file")),
        panelconfig="{prefix}%s%s{panel}.yaml" % (config['dirs']['inputs'], config['dirs']['genepanels']),
        bamstat="{prefix}%s%s/{sample}.bamstat.tsv" % (config['dirs']['intermediate'], config['stepnames']['bamstat'])
    output:
        "{prefix}%s%s/{panel}.yaml/{sample}.tsv" % (config['dirs']['intermediate'], config['stepnames']['genepanel_coverage'])
    log:
        "{prefix}%s%s/{panel}.yaml/{sample}.log" % (config['dirs']['logs'], config['stepnames']['genepanel_coverage'])
    benchmark:
        "{prefix}%s%s/{panel}.yaml/{sample}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['genepanel_coverage'])
    threads:
        1
    run:
        get_gene_panel_coverage(input.panelconfig, input.bamstat, input.agilent, output[0])


rule kraken_bam2fastq:
    input:
        "{prefix}%s%s/{sample}.bam" % (config['dirs']['intermediate'], config['stepnames']['nonhuman'])
    output:
        r1="{prefix}%s%s/{sample}_R1.fastq" % (config['dirs']['intermediate'], config['stepnames']['kraken_bam2fastq']),
        r2="{prefix}%s%s/{sample}_R2.fastq" % (config['dirs']['intermediate'], config['stepnames']['kraken_bam2fastq']),
    conda:
        "envs/spike_kraken.yaml"
    log:
        sam="{prefix}%s%s/{sample}_samtoolssort.log" % (config['dirs']['logs'], config['stepnames']['kraken_bam2fastq']),
        bed="{prefix}%s%s/{sample}_bedtools.log" % (config['dirs']['logs'], config['stepnames']['kraken_bam2fastq']),
    benchmark:
        "{prefix}%s%s/{sample}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['kraken_bam2fastq'])
    threads:
        1
    shell:
        "samtools sort -n {input} 2> {log.sam} | bedtools bamtofastq -i - -fq {output.r1} -fq2 {output.r2} 2> {log.bed}"

rule kraken_report:
    input:
        r1="{prefix}%s%s/{sample}_R1.fastq" % (config['dirs']['intermediate'], config['stepnames']['kraken_bam2fastq']),
        r2="{prefix}%s%s/{sample}_R2.fastq" % (config['dirs']['intermediate'], config['stepnames']['kraken_bam2fastq']),
    output:
        "{prefix}%s%s/{db}_DB/{sample}.k2report" % (config['dirs']['intermediate'], config['stepnames']['kraken_report']),
    # using HHU-HPC module for kraken2 instead of conda, since memory access is extremely slow with conda or self compiled binary
    #conda:
    #    "envs/spike_kraken.yaml"
    log:
        "{prefix}%s%s/{db}_DB/{sample}.log" % (config['dirs']['logs'], config['stepnames']['kraken_report']),
    benchmark:
        "{prefix}%s%s/{db}_DB/{sample}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['kraken_report'])
    threads:
        4
    params:
        fp_db=lambda wildcards: "%s%sKraken/%s_DB" % (config['dirs']['prefix'], config['dirs']['references'], wildcards.db)
    shell:
        "kraken2 --db {params} --threads {threads} --report {output} --paired {input} > /dev/null 2> {log}"


rule kraken_2mpa:
    input:
        "{prefix}%s%s/{db}_DB/{sample}.k2report" % (config['dirs']['intermediate'], config['stepnames']['kraken_report'])
    output:
        "{prefix}%s%s/{db}_DB/{sample}.k2mpa" % (config['dirs']['intermediate'], config['stepnames']['kraken_2mpa'])
    conda:
        "envs/spike_krakentools.yaml"
    log:
        "{prefix}%s%s/{db}_DB/{sample}.log" % (config['dirs']['logs'], config['stepnames']['kraken_2mpa'])
    benchmark:
        "{prefix}%s%s/{db}_DB/{sample}.log" % (config['dirs']['logs'], config['stepnames']['kraken_2mpa'])
    threads:
        1
    shell:
        "kreport2mpa.py -r {input} -o {output} 2> {log}"

rule countreads:
    input:
        "{prefix}%s%s/{sample}_R1.fastq.gz" % (config['dirs']['intermediate'], config['stepnames']['rejoin_samples'])
    output:
        "{prefix}%s%s/{sample}.readcounts" % (config['dirs']['intermediate'], config['stepnames']['kraken_countrawreads'])
    log:
        "{prefix}%s%s/{sample}.log" % (config['dirs']['logs'], config['stepnames']['kraken_countrawreads'])
    benchmark:
        "{prefix}%s%s/{sample}.log" % (config['dirs']['logs'], config['stepnames']['kraken_countrawreads'])
    threads:
        1
    shell:
        "zcat {input} | wc -l > {output} 2> {log}"
